buildscript {
	dependencies {
		classpath 'org.liquibase:liquibase-core:4.33.0'
	}
}

plugins {
	id 'org.springframework.boot' version '4.0.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.liquibase.gradle' version '3.1.0'
	id 'java'
}

group = 'club.tempvs'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

liquibase {
	runList = "main"
	activities {
		main {
			changelogFile 'src/main/resources/db/changelog/changes/base.yml'
			url 'jdbc:postgresql://localhost:5432/tempvs-user'
			referenceUrl 'hibernate:spring:club.tempvs.user.domain?dialect=org.hibernate.dialect.PostgreSQLDialect'
			username 'postgres'
			password 'pass'
		}
	}
}

diff.dependsOn compileJava
generateChangelog.dependsOn compileJava

ext {
	zonkyTestVersion = '2.7.1'
	liquibaseVersion = '4.33.0'
	hibernateVersion = '6.6.43.Final'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-liquibase'
	implementation "org.postgresql:postgresql"
	//lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
    //liquibase
	liquibaseRuntime "org.liquibase:liquibase-core:${liquibaseVersion}"
	liquibaseRuntime "org.liquibase.ext:liquibase-hibernate6:${liquibaseVersion}"
	liquibaseRuntime "info.picocli:picocli:4.7.6"
	liquibaseRuntime "org.postgresql:postgresql"
	liquibaseRuntime("org.springframework.boot:spring-boot-starter-data-jpa") {
		exclude group: "org.hibernate.orm", module: "hibernate-core"
		exclude group: "org.hibernate.orm", module: "hibernate-envers"
	}
	liquibaseRuntime "org.hibernate.orm:hibernate-core:${hibernateVersion}"
	liquibaseRuntime "org.hibernate.orm:hibernate-envers:${hibernateVersion}"
	liquibaseRuntime sourceSets.main.output
	//testing
	testImplementation "io.zonky.test:embedded-database-spring-test:${zonkyTestVersion}"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-webmvc-test'
	testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
}

bootJar {
	archiveBaseName = 'tempvs-user'
	archiveVersion = ''
}

tasks.named('test') {
	useJUnitPlatform()
}

sourceSets {
	integrationTest {
		java {
			srcDir 'src/integration-test/java'
		}
		resources {
			srcDir 'src/integration-test/resources'
		}
		compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
		runtimeClasspath += output + compileClasspath
	}
}

tasks.register('integrationTest', Test) {
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

configurations.liquibaseRuntime {
	resolutionStrategy {
		force "org.hibernate.orm:hibernate-core:${hibernateVersion}"
		force "org.hibernate.orm:hibernate-envers:${hibernateVersion}"
	}
}

tasks.named('integrationTest') {
	useJUnitPlatform()
}
